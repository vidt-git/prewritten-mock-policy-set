# This policy requires resources of type 'aws_db_instance' have attribute 'auto_minor_version_upgrade' set to true.

# Copyright (c) HashiCorp, Inc.
# SPDX-License-Identifier: BUSL-1.1

# Imports

import "tfplan/v2" as tfplan
import "tfresources" as tf
import "report" as report
import "collection" as collection
import "collection/maps" as maps

# Constants

const = {
	"policy_name":                "rds-minor-version-upgrade-enabled",
	"message":                    "Attribute 'auto_minor_version_upgrade' must be set to true for 'aws_db_instance' resources. Refer to https://docs.aws.amazon.com/securityhub/latest/userguide/rds-controls.html#rds-13 for more details.",
	"resource_aws_db_instance":   "aws_db_instance",
	"auto_minor_version_upgrade": "auto_minor_version_upgrade",
}

# Variables

resources = tf.plan(tfplan.planned_values.resources).type(const.resource_aws_db_instance).resources
violations = collection.reject(resources, func(res) {
	val = maps.get(res, "values.auto_minor_version_upgrade", false)
	return val is true
})

summary = {
	"policy_name": const.policy_name,
	"violations": map violations as _, v {
		{
			"address":        v.address,
			"module_address": v.module_address,
			"message":        const.message,
		}
	},
}

# Outputs

print(report.generate_policy_report(summary))

# Rules

main = rule {
	violations is empty
}
